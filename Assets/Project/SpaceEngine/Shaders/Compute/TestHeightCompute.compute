#define threadsPerGroup_X 32
#define threadsPerGroup_Y 32
#define nVerticesPerSide 128

#include "Assets/Project/SpaceEngine/Shaders/TCCommonAsteroid.cginc"

struct GenerationConstants
{
    float scale;
    float noiseSeaLevel;
    float planetRadius;
	float spacing;
    float terrainMaxHeight;

    float3 cubeFaceEastDirection;
    float3 cubeFaceNorthDirection;
    float3 patchCubeCenter;
};

struct OutputStruct
{
	float noise;

	float3 patchCenter;

    float4 pos;
};

RWStructuredBuffer<GenerationConstants> generationConstants;
RWStructuredBuffer<OutputStruct> patchPreOutput;
RWStructuredBuffer<OutputStruct> patchOutput;

#pragma kernel CSMain
[numthreads(threadsPerGroup_X, threadsPerGroup_Y, 1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    int outBuffOffset = id.x + id.y * nVerticesPerSide;
	
	GenerationConstants constants = generationConstants[0];
	
	float eastValue = (id.x - ((nVerticesPerSide - 1) / 2.0)) * constants.spacing;
	float northValue = (id.y - ((nVerticesPerSide - 1) / 2.0)) * constants.spacing;

	float3 cubeCoordEast = constants.cubeFaceEastDirection * eastValue;
    float3 cubeCoordNorth = constants.cubeFaceNorthDirection * northValue;

	float3 cubeCoord = cubeCoordEast + cubeCoordNorth + constants.patchCubeCenter;

	float3 patchCenter = normalize(constants.patchCubeCenter) * constants.planetRadius;
	float3 patchNormalizedCoord = normalize(cubeCoord);

	float3 patchCoord =  constants.planetRadius * patchNormalizedCoord;
	float3 patchCoordCentered = patchCoord - patchCenter;

	float noise = HeightMapAsteroid(0.001 * patchCoord, 0.5, 0.75, 0.45, 0.25, 0.35);
    float height = ((noise * 2) - 1) * constants.terrainMaxHeight;

	patchCoordCentered += patchNormalizedCoord * height;

    float4 output = float4(patchCoordCentered.x, patchCoordCentered.y, patchCoordCentered.z, 0);

	patchPreOutput[outBuffOffset].noise = noise;
	patchPreOutput[outBuffOffset].patchCenter = patchCenter;
    patchPreOutput[outBuffOffset].pos = output;
}

RWTexture2D<float4> Height;
RWTexture2D<float4> Normal;

#pragma kernel CSSubMain
[numthreads(threadsPerGroup_X, threadsPerGroup_Y, 1)]
void CSSubMain (uint3 id : SV_DispatchThreadID)
{
	int outBuffOffset = id.x + id.y * nVerticesPerSide;
	
	GenerationConstants constants = generationConstants[0];

	float noise = patchPreOutput[outBuffOffset].noise;

	float4 pos = patchPreOutput[outBuffOffset].pos;

	Height[id.xy] = float4(pos.xyz, noise);

	float left = patchPreOutput[(id.x - 1) + id.y * nVerticesPerSide].noise * 2;
	float right = patchPreOutput[(id.x + 1) + id.y * nVerticesPerSide].noise * 2;
	float up = patchPreOutput[id.x + (id.y - 1) * nVerticesPerSide].noise * 2;
	float down = patchPreOutput[id.x + (id.y + 1) * nVerticesPerSide].noise * 2;

	float xdelta = ((left - right) + 1) * 0.5;
	float ydelta = ((up - down) + 1) * 0.5;

	Normal[id.xy] = float4(xdelta, ydelta, 1, 0);

	patchOutput[outBuffOffset] = patchPreOutput[outBuffOffset];
}